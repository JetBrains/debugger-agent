plugins {
    id 'java'
}
apply plugin: 'maven-publish'

// BUILD_NUMBER env is set by TeamCity
version = System.getenv("BUILD_NUMBER") ?: "1.0-SNAPSHOT"
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    implementation files("lib/asm-capture-9.6.1.jar")
}

task mainJar(type: Jar) {
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    with jar
    manifest {
        attributes(
                "Premain-Class": "com.intellij.rt.debugger.agent.DebuggerAgent",
                "Can-Retransform-Classes": "true",
                "Boot-Class-Path": "debugger-agent.jar",
                "Implementation-Version": project.version,
                // BUILD_VCS_NUMBER env is set by TeamCity
                "Implementation-SCM-Revision": System.getenv("BUILD_VCS_NUMBER") ?: "LOCAL-SNAPSHOT",
                "Implementation-SCM-Url": "https://github.com/JetBrains/debugger-agent",
        )
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.java
}

artifacts {
    archives mainJar, sourceJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            artifactId 'debugger-agent'
            groupId 'org.jetbrains.intellij.deps'
            version "${project.version}"
            artifact mainJar
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"
            credentials {
                username = System.getenv('INTELLIJ_DEPENDENCIES_BOT')
                password = System.getenv('INTELLIJ_DEPENDENCIES_TOKEN')
            }
        }
    }
}
